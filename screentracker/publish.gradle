apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'com.orhunkupeli.screentracker'
version '0.0.1'

project.ext {
    mavGitUrl = 'https://github.com/o7k7/ScreenTracker'
    mavProjectName = 'ScreenTracker'
    mavLibraryLicenses = ["MIT":""]
    mavLibraryDescription = "Track your Activity and Fragment Lifecycles from one place"
    mavPublishToMavenLocal = true
    mavSiteUrl = 'https://github.com/o7k7/ScreenTracker'
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/screentracker-release.aar")  {
                builtBy tasks.getByName("buildJar")
            }
            groupId
            artifactId 'screentracker'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'ScreenTracker'
        name = 'ScreenTracker'
        description = "Track your Activity and Fragment Lifecycles from one place"
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/o7k7/ScreenTracker'
        version {
            name = this.version
            desc = "Version ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}

apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'